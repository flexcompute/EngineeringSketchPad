# Design Parameters:
despmtr   tunnelHeight 20.00000
despmtr   tunnelLength 50.00000
cfgpmtr   series       8412
despmtr   chord        1.00000
cfgpmtr   plane        1 # 1 x-z, 2 x-y 

# Switch to toggle sharp trailing edge on airfoil
despmtr sharpTE 1

# Global attributes:
attribute capsIntent $CFD
attribute capsAIM $fun3dAIM;su2AIM;egadsTessAIM;aflr2AIM;delaundoAIM;refineAIM;metrisAIM #CFD Analysis
attribute capsMeshLength 1

# Build wind tunnel
box       -tunnelLength/2  -tunnelHeight/2  0      tunnelLength  tunnelHeight  0   
store     Tunnel 0 0

# Build Airfoil cross section and extrude
udprim    naca   Series series sharpte sharpTE
scale     chord

store     Airfoil 0 0


# Restore bodies and execute boolean operator
restore   Tunnel 0
restore   Airfoil 0
subtract  none   1   0 


select face 1
    attribute capsGroup $2DSlice # Face needs to marked with a capsGroup - this may not be necessary in the future
    attribute capsMesh $2DSlice
    
# Set capsGroup names around boundaries
select    edge   1
    attribute capsGroup   $TunnelWall
     attribute capsMesh   $TunnelWall
select    edge   2
    attribute capsGroup   $OutFlow
    attribute capsMesh   $OutFlow
select    edge   3
    attribute capsGroup   $InFlow
    attribute capsMesh   $InFlow
select    edge   4
    attribute capsGroup   $TunnelWall
    attribute capsMesh   $TunnelWall

select edge 5
    attribute capsGroup $Airfoil
    attribute capsMesh $Airfoil

select edge 6
    attribute capsGroup $Airfoil
    attribute capsMesh $Airfoil

ifthen sharpTE eq 0 

    select edge 7
        attribute capsGroup $Airfoil
        attribute capsMesh $Airfoil

endif

ifthen plane EQ 1
  rotatex   90   0   0 # Put body in x-z plane 
endif

end
